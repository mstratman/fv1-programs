; single head tape echo + reverb
;
; Graham Biswell <graham@gbiswell.com>
;
; reverb code is a 2 tap version of the Spin OEM1 Vocal Plate
; 
; pot 0 echo delay time
; pot 1 echo feedback
; pot 2 damping
;

; ***********************
; delay declarations
equ	DelayLen	20000
equ	sm0len		40
equ	sm1len		20

mem	DelayMem	DelayLen
mem	sm0		sm0len
mem	sm1		sm1len

; reverb memory 
mem     iap1		156
mem     iap2		223
;
mem     ap1		1251
mem     ap1b		1751
mem     ap2		1443
mem     ap2b		1343
;
mem     rdel1		3559
mem     rdel2		2945

; general smothing filter coeff
equ	smooth		0.001

; pots
equ	TimePot		pot0
equ	FbackPot	pot1
equ	DampPot		pot2

; level of Delay signal in output
equ	DelayMix	0.7
; amount of reverb to mix in when fixed amount
equ	RvbMixLvl	0.4

; levels of signals into reverb
; amount of dry signal to mix into reverb
equ	SigIntoRvb	0.8
; amount of delay signal to mix into reverb
equ	DelayIntoRvb	0.2

; damping lpf & hpf cutoff freqs & shelf in feedback
; shelf (-1 = infinite shelf, -0.5 = 6dB)
equ	DampLpf		0.60
equ	DampLpfSlf	0.50
equ	DampHpf		0.20
equ	DampHpfSlf	-0.6

; amount to scale fb pot by
equ	FbackScale	0.75
equ	DampScale	0.80

equ	feedback	reg1
equ	SigIn		reg2
equ	offset		reg3
equ	DelaySig	reg4
equ	temp		reg5
equ	rnd		reg6
equ	damping		reg7
equ	time		reg8
equ	SigAvg		reg9
equ	lpf1		reg10
equ	hpf1		reg11
equ	lpf2		reg12

; ***********************
; reverb declarations
equ	rhpf1		reg13
equ	rhpf2		reg14
equ	rlpf1		reg15
equ	rlpf2		reg16
equ	rdamp		reg17
equ	rt		reg18
equ	iapout		reg19

;constants:

equ	kfh		0.01
equ	kfl		0.4
equ	kiap		0.5
equ	klap		0.6

; ***********************
; initialisation
skp	run, loop
wlds	sin0, 3, sm0len/2
wlds	sin1, 32, sm1len/2
sof     0, 0.8
wrax    rt, 0
sof     0, 0.3
wrax    rdamp, 0

; main loop
loop:

; ***********************
; pots
rdax	DampPot, 1
sof	DampScale, 0.1
rdfx	damping, smooth
wrax	damping, 0

; read feedback level pot
; damping factor is based from feedback - more feedback, slightly more damping
rdax	FbackPot, FbackScale
rdfx	feedback, smooth
wrax	feedback, 0

; read delay time pot
rdax	TimePot, 1
sof	0.99, 0.01
wrax	time, 0

; ***********************
; delay
; start with a small amount of randomness to the delay time
cho	rdal, cos0
cho	rdal, cos1
and	7

or	DelayLen * 256
mulx	time
rdfx	offset, smooth
wrax	offset, 0

; get input signal
rdax	adcl, 0.5
rdax	adcr, 0.5
wrax	SigIn, 0

;; tape head
rdax	offset, 1
wrax	addr_ptr, 0
rmpa	1

; some wow & flutter
wra	sm0, 0
cho	rda, sin0, sin | reg | compc, sm0^
cho	rda, sin0, sin, sm0^+1
wra	sm1, 0
cho	rda, sin1, cos | reg | compc, sm1^
cho	rda, sin1, cos, sm1^+1
wrax	DelaySig, 1

; mix delay signal with input
mulx	feedback
rdax	SigIn, 1

; soft knee limiter
wrax	temp, 1
mulx	temp
rdfx	SigAvg, smooth
wrax	SigAvg, 0

rdax	time, -1
sof	0.06, 0.25
rdax	SigAvg, 1
log	-0.4, -0.17
exp	1, 0
mulx	temp
sof	-2, 0
sof	-2, 0
sof	1.5, 0

; damping
wrax	temp, 1
rdfx	lpf1, DampLpf
wrlx	lpf1, DampLpfSlf
rdfx	hpf1, DampHpf
wrhx	hpf1, DampHpfSlf
rdfx	lpf2, 0.4
wrlx	lpf2, -0.6
rdax	temp, -1
mulx	damping
rdax	temp, 1

; write to memory
wra	0, 0

;******************************
; reverb
;run a mix of dry & delay signal into 2 all passes:

rdax	temp, DelayIntoRvb
rdax	SigIn, SigIntoRvb
rda     iap1#,kiap
wrap    iap1,-kiap
rda     iap2#,kiap
wrap    iap2,-kiap
wrax    iapout,0                ;write to register for ring injection

;now do reverb ring, use temp as reg for filtering:

;aps into delay1:
rda     rdel2#,1                ;read previous delay
mulx    rt              ;multiply by reverb time coefficient
rdax    iapout,1        ;read left input from input allpass filter bank
rda     ap1#,klap       ;do an allpass filter
wrap    ap1,-klap
rda     ap1b#,klap      ;do second all pass filter
wrap    ap1b,-klap
wrax    temp,1          ;write to temp, keep in acc
rdfx    rlpf1,kfl       ;low pass filter
wrlx    rlpf1,-1
rdfx    rhpf1,kfh       ;high pass filter
wrhx    rhpf1,-1
rdax    temp,-1
mulx    rdamp           ;crossfade between filter and no filter
rdax    temp,1
wra     rdel1,0         ;write to next delay, clear accumulator

;aps into delay2:
rda     rdel1#,1
mulx    rt
rdax    iapout,1
rda     ap2#,klap
wrap    ap2,-klap
rda     ap2b#,klap
wrap    ap2b,-klap
wrax    temp,1
rdfx    rlpf2,kfl
wrlx    rlpf2,-1
rdfx    rhpf2,kfh
wrhx    rhpf2,-1
rdax    temp,-1
mulx    rdamp
rdax    temp,1
wra     rdel2, 0

rda	rdel1, RvbMixLvl
rdax	DelaySig, DelayMix
wrax	dacl, 0

rda	rdel2, RvbMixLvl
rdax	DelaySig, DelayMix
wrax	dacr, 0

;smear allpass filters in reverb ring:
cho     rda,sin0,reg|compc,ap1+100      ;sin0
cho     rda,sin0,0,ap1+101
wra     ap1+200,0

cho     rda,sin1,cos|compc,ap2+100      ;cos0
cho     rda,sin1,cos,ap2+101
wra     ap2+200,0
