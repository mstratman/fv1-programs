; Reverb + Shimmer (Version 6) by DrAlx (Alex Lawrow)
;
; This routine is based on Mick Taylor's (Ice-9s) reverb loop
; and shimmer code with some changes such as:
;
;  1) Prime numbers for delay line lengths.
;  2) More linear mapping of pot sweep to reverb time.
;  3) Anti-aliasing filter before the pitch-shifter.
;  4) Shimmer level is controlled by feeding both the input signal
;      and the reverb output into the pitch-shifter in varying amounts.
;  5) Pitch-shifted signal is fed into a delay line to give a couple of short delays.
;     This lets the shimmer effect build up slowly in time.
;  6) Output is 100% wet.  Uncomment line at bottom to add dry signal.

;POT0 = Reverb time (0 to 10 seconds).
;POT1 = Amount of treble in reverb loop.
;POT2 = Shimmer level

MEM	tmp	1	; Temp memory for octave-up
MEM	octave	4096	; Delay line for octave-up
MEM	echos	14831	; Delay line for echos after octave up

; 4 AP filters, just before the reverb loop
MEM	ap1	137
MEM	ap2	257
MEM	ap3	563
MEM	ap4	761
; The reverb loop AP filters
MEM	lap1a	1423
MEM	lap1b	1949
MEM	lap2a	1759
MEM	lap2b	1889
; The reverb loop delay lines
MEM	d1	2437
MEM	d2	2647

; Registers
EQU	temp		REG0	; Temp register for filter routines and other stuff
EQU	dry		REG1	; Dry signal
EQU	krt		REG2	; Reverb decay coefficient
EQU	apout		REG3	; Output of input APs.  For injection into reverb loop
EQU	rev_out		REG4	; Reverb output
EQU	kd		REG5	; Coefficient for shelving LPF filters in reverb loop
EQU	lp_antialias	REG6	; Anti-alias LPF before octave up
EQU	lp1		REG7	; Loop LPF 1
EQU	lp2		REG8	; Loop LPF 2
EQU	hp1		REG9	; Loop HPF 1
EQU	hp2		REG10	; Loop HPF 2
EQU	octave_dry	REG11	; Amount of dry fed into octave-up
EQU	octave_rev	REG12	; Amount of reverb fed into octave-up

;-------------- Initialize LFOs etc -------------------
SKP	RUN,end_init
WLDS	SIN0,25,100		; Sin LFO for reverb "smoothing". (25 ==> ~1Hz)
WLDR	RMP0,16384,4096	; Ramp LFO for octave up.  (16384 ==> octave up)
end_init:
;-------------------------------------------------

;--------- Map control pots to gain factors ------------
; Map POT0 to reverb time (0 to 10 seconds)
;
; The shortest delay tap in the reverb loop is at 74.27ms.
; So the loop decay factor (krt) relates to RT60 reverb time (T) as follows (I think):
;      krt = 0.001 ^ ( 0.07427 / T ),				Eq(1)
;
; As POT0 goes from 0 to 1, try to make T increase linearly from 0s to 10s.
;      krt = 0.001 ^ ( 0.07427 / ( 0.001 + 10 * POT0 ) ), 	Eq(2)
;
; POT0   RT60(s)   krt
; 0         0.001     0.00
; 1.0      10.00     0.95
;
; Eq(2) would be a nightmare to implement, so use this approximation
;    krt = 0.95 * POT0 ^ ( 0.185 * ( 1 - POT0 ) )		Eq(3)

RDAX	POT0,1		; Read POT0 (full sweep is 0 to 1)
SOF	-0.185,0.185
WRAX 	temp,0
RDAX	POT0,1
LOG	1,0
MULX	temp
EXP	0.95,0
WRAX 	krt,0		; krt = 0.95 * POT0 ^ ( 0.185 * ( 1 - POT0 ) )

; Map POT1 to control the shelving LPFs in the reverb loop
RDAX	POT1,1			; Read POT1
SOF	0.5,-0.5
WRAX	kd,0			; kd = -0.5 to 0 (i.e. clockwise to increase treble)

; Map POT2 to shimmer gain in two stages.
; First half turn increases reverb output into octaveup (from a factor of 0 to 0.2).
; Next half turn increases dry signal into the octaveup (from a factor of 0 to 1)
SOF	0,0.2
WRAX	octave_rev,0		; Set default octave_rev to maximum (0.2)
RDAX 	POT2,1			; Read POT2 (full sweep is 0 to 1)
SOF	1,-0.5			; ACC = -0.5 to 0.5
SKP	gez,set_octave_dry	; If >=0, set octave_dry, else overwrite default octave_rev and zero octave_dry
SOF	-2,0			; ACC = (1-x)  where x increases 0 to 1
WRAX 	temp,1
MULX 	temp			; ACC = (1-x)^2
MULX 	temp			; ACC = (1-x)^3
MULX 	temp			; ACC = (1-x)^4
SOF	-0.2,0.2			; ACC = 0.2 -  0.2 * (1-x)^4
WRAX	octave_rev,0		; Write to octave_rev, then clear ACC to zero octave_dry
set_octave_dry:
sof	1.999,0
WRAX	octave_dry,0		; Set octave_dry (0 to 1)
;-------------------------------------------------


;---------- Take average of L and R inputs -----------
RDAX	ADCL,0.5
RDAX	ADCR,0.5
WRAX	dry,1			; Write to dry
;-------------------------------------------------


;----------- Make input signal for octave up ----------
MULX	octave_dry		; ACC already contains dry
WRAX	temp,0			; temp = dry * octave_dry
RDAX	rev_out,1
MULX	octave_rev
RDAX	temp,1			; ACC = dry * octave_dry  + rev_out * octave_rev
;-------------------------------------------------


;--------- Anti-alias LPF before the octave up ---------
RDFX	lp_antialias, 0.544		; 4096 Hz ==> 1 - exp(-2*pi*4096/32768) ==> 0.544
WRAX	lp_antialias, 1
WRA	octave,0			; Write to octave up
;-------------------------------------------------


;------------- Octave up fed into delay --------------
CHO	RDA,RMP0,REG|COMPC,octave
CHO	RDA,RMP0,,octave+1
WRA	tmp,0
CHO	RDA,RMP0,RPTR2|COMPC,octave
CHO	RDA,RMP0,RPTR2,octave+1
CHO	SOF,RMP0,NA|COMPC,0
CHO	RDA,RMP0,NA,tmp

WRA	echos,0		; Write to start of echos delay line
;-------------------------------------------------


;------------- Make reverb input signal --------------
RDA	echos^,1		; Read echo from middle of delay line
RDA	echos#,1		; Add echo from end of delay line
RDAX	dry,1		; Add in dry signal
;-------------------------------------------------


;----------- 4 APs before the reverb loop ------------
RDA	ap1#,0.5
WRAP	ap1,-0.5
RDA	ap2#,0.5
WRAP	ap2,-0.5
RDA	ap3#,0.5
WRAP	ap3,-0.5
RDA	ap4#,0.5
WRAP	ap4,-0.5
WRAX	apout,0		; apout will get injected (twice) into the reverb loop

;---------------- Begin reverb loop -----------------
RDA	d2#,1		; Read from end of d2
MULX	krt		; Scale by krt
RDAX	apout,0.5	; Inject 0.5 * apout
RDA	lap1a#,0.5	; 1st loop AP filter before d1
WRAP	lap1a,-0.5	; ...
RDA	lap1b#,0.5	; 2nd loop AP filter before d1
WRAP	lap1b,-0.5	; ...

;Adjustable shelved LPF (DC Gain = 1, Corner = 2700 Hz)
WRAX	temp,1		; Save input
RDFX	lp1,0.404	; 0.404 = 1 - exp(-2*pi*2700/32768)
WRHX	lp1,-1		;
MULX	kd		; Multiply by damping coefficient
RDAX	temp,1		; Add back input

;Shelved HPF (DC Gain = 0.5, Corner = 53 Hz)
RDFX	hp1,0.01		; 0.01 = 1 - exp(-2*pi*53/32768)
WRHX	hp1,-0.5		; -0.5 = DC Gain - 1
WRA	d1,0		; Write to start of d1

RDA	d1#,1		; Read from end of d1
MULX	krt		; Scale by krt
RDAX	apout,0.5	; Inject 0.5 * apout
RDA	lap2a#,0.5	; 1st loop AP filter before d2
WRAP	lap2a,-0.5	; ...
RDA	lap2b#,0.5	; 2nd loop AP filter before d2
WRAP	lap2b,-0.5	; ...

;Adjustable shelved LPF (DC Gain = 1, Corner = 2700 Hz)
WRAX	temp,1		; Save input
RDFX	lp2,0.404	; 0.404 = 1 - exp(-2*pi*2700/32768)
WRHX	lp2,-1		;
MULX	kd		; Multiply by damping coefficient
RDAX	temp,1		; Add back input

;Shelved HPF (DC Gain = 0.5, Corner = 53 Hz)
RDFX	hp2,0.01		; 0.01 = 1 - exp(-2*pi*53/32768)
WRHX	hp2,-0.5		; -0.5 = DC Gain - 1
WRA	d2,0		; Write to start of d2
;---------------- End reverb loop ------------------


;-- Reverb smoothing (modulate delay lines in the loop) --
CHO	RDA,SIN0,REG|SIN|COMPC,d1+100
CHO	RDA,SIN0,SIN,d1+101
WRA	d1+200,0

CHO	RDA,SIN0,REG|COS|COMPC,d2+100
CHO	RDA,SIN0,COS,d2+101
WRA	d2+200,0
;-------------------------------------------------


;------------ Make reverb output signal --------------
; Take output taps from start of loop delays
RDA	d1,1
RDA	d2,1
WRAX	rev_out,1		; Save reverb output to register
;-------------------------------------------------

;RDAX	dry,1		; Add dry signal
WRAX	DACL,0
